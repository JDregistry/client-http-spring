plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.2.70'
}

group 'jdregistry.client'
version '0.0.5'

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()

    // That is where the JDregistry is currently hosted
    maven {
        url  "https://dl.bintray.com/lukaszimmermann/jdregistry"
    }
}

configurations {
    ktlint
}

/*
 * Plugins
 */
apply plugin: "java"
apply plugin: 'kotlin'
apply plugin: 'maven-publish'

dependencies {
    ktlint "com.github.shyiko:ktlint:0.28.0"

    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: '1.2.70'

    // The Docker Registry Client implementation that the PHT library depends on
    compile group: 'jdregistry.client', name: 'http', version: '0.0.5'
    compile group: 'org.springframework', name: 'spring-web', version: '5.0.9.RELEASE'

    testCompile group: 'jdregistry', name: 'test', version: '0.0.5'
}


publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            version '0.0.5'
            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each() {
                    it.scope*.value = 'compile'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenPublication {
        destination = file("$buildDir/libs/http-spring-0.0.5.pom")
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}


/*
 * Enable source and javadoc for Maven publish
 */

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

/*
 * The Linter
 */
task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}
